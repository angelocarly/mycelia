#version 450

layout ( local_size_x = 16, local_size_y = 16, local_size_z = 1 ) in;

layout( binding = 0, rgba8 ) uniform image2D image;

struct Pos {
    vec3 p;
    float v;
};
layout(std430, binding = 1 ) buffer Locations {
    ivec4 v;
    Pos pos[];
} edges;

layout( push_constant ) uniform PushConstants
{
    mat4 transform;
} constants;

vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d )
{
    return a + b*cos( 6.28318*(c*t+d) );
}

void dda_line( ivec2 p0, ivec2 p1, vec4 color )
{
    ivec2 delta = abs( p1 - p0 );
    ivec2 step = ivec2( sign( p1.x - p0.x ), sign( p1.y - p0.y ) );
    ivec2 offset = delta / 2;
    ivec2 p = p0;
    ivec2 end = p1;
    if( delta.x >= delta.y )
    {
        for( int i = 0; i < delta.x; i++ )
        {
            imageStore( image, p, color );
            offset.y += delta.y;
            if( offset.y >= delta.x )
            {
                offset.y -= delta.x;
                p.y += step.y;
            }
            p.x += step.x;
        }
    }
    else
    {
        for( int i = 0; i < delta.y; i++ )
        {
            imageStore( image, p, color );
            offset.x += delta.x;
            if( offset.x >= delta.y )
            {
                offset.x -= delta.y;
                p.x += step.x;
            }
            p.y += step.y;
        }
    }
}


void main()
{
    ivec2 p = ivec2( gl_GlobalInvocationID.xy );
    ivec2 screenSize = imageSize( image );
    if( p.x > screenSize.x || p.y > screenSize.y )
    {
        return;
    }

    if( p.x >= edges.v.x || p.y > 1 ) return;
    int id = p.x;

    vec2 pos = vec2( float( p.x ) / float( screenSize.x ), float( p.y ) / float( screenSize.y ) ) - 0.5f;

    float t = edges.pos[id*2].v;
    vec3 color = palette( t, vec3( 0.9, 0.4, 0.5 ), vec3( 0.4, 0.7, 0.5 ), vec3( 1.0, 1.2, 0.0 ), vec3( 0.5, 0.60, 0.25 ) );
    color *= .7f;

    vec4 p1 = constants.transform * vec4(edges.pos[id*2].p, 1);
    vec4 p2 = constants.transform * vec4(edges.pos[id*2+1].p, 1);
    p1 /= p1.w;
    p2 /= p2.w;

    p1.x *= screenSize.x;
    p1.y *= screenSize.y;
    p1.x += screenSize.x / 2.;
    p1.y += screenSize.y / 2.;

    p2.x *= screenSize.x;
    p2.y *= screenSize.y;
    p2.x += screenSize.x / 2.;
    p2.y += screenSize.y / 2.;

    ivec2 ip1 = ivec2( p1.xy );
    ivec2 ip2 = ivec2( p2.xy );
    dda_line(ip1, ip2, vec4(color, 1));
    dda_line(ip1 + ivec2(1,1), ip2 + ivec2(1,1), vec4(color, 1));
    dda_line(ip1 + ivec2(-1,-1), ip2 + ivec2(-1,-1), vec4(color, 1));
    dda_line(ip1 + ivec2(-1,1), ip2 + ivec2(-1,1), vec4(color, 1));
    dda_line(ip1 + ivec2(1,-1), ip2 + ivec2(1,-1), vec4(color, 1));
}