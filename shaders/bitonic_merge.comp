#version 450

layout ( local_size_x = 128, local_size_y = 1, local_size_z = 1 ) in;

struct Node {
    vec4 p;
    ivec4 i;
};
layout(std430, binding = 0 ) buffer LocationsIn {
    Node nodes[];
} node_buffer_in;

/* A buffer with all nodes contained by the cells */
struct Ordering {
    uint node_id;
    uint cell_id;
    int offset1;
    int offset2;
};
layout(std430, binding = 2 ) buffer SpatialOrdering {
    Ordering lookup[];
} spatial_ordering;

/* Maps a cell-id to the respective nodes index, as well as element count */
struct CellLookup {
    int order_index;
};
layout(std430, binding = 3 ) buffer SpatialLookup {
    CellLookup cells[];
} spatial_lookup;

layout( push_constant ) uniform PushConstants
{
    uint node_count;
    uint group_width;
    uint group_height;
    uint step_index;
} constants;

void main()
{
    ivec2 p = ivec2( gl_GlobalInvocationID.xy );
    int id = p.x;

    // Algorithm comes from Sebastian Lague
    // https://www.youtube.com/watch?v=rSKMYc1CQHE&t=1478s

    uint i = id;
    uint h = i & (constants.group_width - 1);
    uint index_low = h + (constants.group_height + 1) * ( i / constants.group_width );
    uint index_high = index_low + (constants.step_index == 0 ? constants.group_height - 2 * h : (constants.group_height + 1) / 2);


    if( index_high >= constants.node_count ) return;

    Ordering value_low = spatial_ordering.lookup[index_low];
    Ordering value_high = spatial_ordering.lookup[index_high];

    if( value_low.cell_id > value_high.cell_id ) {
        spatial_ordering.lookup[index_low] = value_high;
        spatial_ordering.lookup[index_high] = value_low;
    }

}